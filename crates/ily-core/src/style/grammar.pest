// a parser for style sheets in ily

WHITESPACE = _{ " " | "\t" | "\n" | "\r" }
COMMENT = _{ "/*" ~ (!"*/" ~ ANY)* ~ "*/" }

// atoms
Identifier = @{ (ASCII_ALPHA | "-" | "_") ~ (ASCII_ALPHANUMERIC | "-" | "_")* }
Number = @{ ASCII_DIGIT+ ~ ("." ~ ASCII_DIGIT+)? ~ (^"e" ~ ("-" | "-") ~ ASCII_DIGIT+)? }

// values
Px = ${ Number ~ "px" }
Pt = ${ Number ~ "pt" }
HexColor = @{ "#" ~ ASCII_HEX_DIGIT{6} }
RgbColor = @{ "rgb(" ~ Number ~ "," ~ Number ~ "," ~ Number ~ ")" }
RgbaColor = @{ "rgba(" ~ Number ~ "," ~ Number ~ "," ~ Number ~ "," ~ Number ~ ")" }
Color = { HexColor | RgbColor | RgbaColor }
String = { Identifier }

// transitions
Transition = _{ Number ~ "s" }

// attribute
Transitionable = _{ Px | Pt | Color }
AttributeValue = { Transitionable ~ Transition? | String }
Attribute = { Identifier ~ ":" ~ AttributeValue ~ ";" }

// selector
State = { ":" ~ Identifier }
Element = { (Identifier | Wildcard) ~ State* }
Class = { "." ~ Identifier }
Wildcard = { "*" }
Selector = { (Element+ ~ Class*) | (Element* ~ Class+) }

// stylesheet
StyleRule = { Selector ~ "{" ~ Attribute* ~ "}" }
Style = { SOI ~ StyleRule* ~ EOI }
