// a parser for style sheets in ily

WHITESPACE = _{ " " | "\t" | "\n" | "\r" }
COMMENT = _{ "/*" ~ (!"*/" ~ ANY)* ~ "*/" }

// atoms
identifier = @{ (ASCII_ALPHA | "-" | "_") ~ (ASCII_ALPHANUMERIC | "-" | "_")* }
number = @{ ASCII_DIGIT+ ~ ("." ~ ASCII_DIGIT+)? ~ (^"e" ~ ("-" | "-") ~ ASCII_DIGIT+)? }

// values
px = ${ number ~ "px" }
pt = ${ number ~ "pt" }
hex_color = @{ "#" ~ ASCII_HEX_DIGIT{6} }
color = { hex_color }
string = { identifier }

// attribute
value = _{ px | pt | color | string }
attribute = { identifier ~ ":" ~ value ~ ";" }

// selector
element = { identifier }
class = { "." ~ identifier }
selector = { element? ~ class* }

// stylesheet
style_rule = { selector ~ "{" ~ attribute* ~ "}" }
stylesheet = { SOI ~ style_rule* ~ EOI }
