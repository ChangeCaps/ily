// a parser for style sheets in ily

WHITESPACE = _{ " " | "\t" | "\n" | "\r" }
COMMENT = _{ "/*" ~ (!"*/" ~ ANY)* ~ "*/" }

// atoms
Identifier = @{ (ASCII_ALPHA | "-" | "_") ~ (ASCII_ALPHANUMERIC | "-" | "_")* }
Number = @{ ASCII_DIGIT+ ~ ("." ~ ASCII_DIGIT+)? ~ (^"e" ~ ("-" | "-") ~ ASCII_DIGIT+)? }

// values
Px = ${ Number ~ "px" }
Pt = ${ Number ~ "pt" }
HexColor = @{ "#" ~ ASCII_HEX_DIGIT{6} }
Color = { HexColor }
String = { Identifier }

// attribute
AttributeValue = _{ Px | Pt | Color | String }
Attribute = { Identifier ~ ":" ~ AttributeValue ~ ";" }

// selector
State = { ":" ~ Identifier }
Element = { Identifier ~ State* }
Class = { "." ~ Identifier }
Wildcard = { "*" }
Selector = { (Element+ ~ Class*) | (Element* ~ Class+) | Wildcard }

// stylesheet
StyleRule = { Selector ~ "{" ~ Attribute* ~ "}" }
Style = { SOI ~ StyleRule* ~ EOI }
